{-# OPTIONS --safe --without-K #-}
open import Semantics.Kripke.Frame using (MFrame)

module Semantics.Presheaf.Possibility
  {C    : Set}
  {_⊆_  : (Γ Δ : C) → Set}
  {_R_  : (Γ Δ : C) → Set}
  (MF  : MFrame C _⊆_ _R_)
  (let open MFrame MF)
  where

open import Relation.Binary.PropositionalEquality using (_≡_; subst; cong; cong₂) renaming (refl to ≡-refl; sym to ≡-sym; trans to ≡-trans)
open import Relation.Binary using (Reflexive; Symmetric; Transitive; IsEquivalence; Setoid)
import Relation.Binary.Reasoning.Setoid as EqReasoning

open import Data.Product using (∃; _×_; _,_; -,_) renaming (proj₁ to fst; proj₂ to snd)
open import Data.Product using () renaming (∃ to Σ; _×_ to _∧_)

open import Semantics.Presheaf.Base IF

open import Semantics.Category.EndoFunctor

private
  variable
    w w' w'' v : C
    𝒫 𝒫'    : Psh
    𝒬 𝒬'     : Psh
    ℛ ℛ' ℛ'' : Psh

-- type \di2 for ◇
record ◇'-Fam (𝒫 : Psh) (w : C) : Set where
  constructor elem
  field
    triple : Σ λ v → (w R v) × 𝒫 ₀ v

open ◇'-Fam public

record _◇'-≋_ {𝒫 : Psh} {w : C} (x y : ◇'-Fam 𝒫 w) : Set where
  constructor proof
  field
    pw : let (v , r , p) = x .triple ; (v' , r' , p') = y. triple
      in ∃ λ v≡v' → subst (_ R_) v≡v' r ≡ r' ∧ subst (𝒫 ₀_) v≡v' p ≋[ 𝒫 ] p'

open _◇'-≋_ public

◇'-≋-refl : Reflexive (_◇'-≋_ {𝒫} {w})
◇'-≋-refl {𝒫} = proof (≡-refl , ≡-refl , ≋[ 𝒫 ]-refl)

◇'-≋-sym : Symmetric (_◇'-≋_ {𝒫} {w})
◇'-≋-sym {𝒫} (proof (≡-refl , ≡-refl , p)) = proof (≡-refl , ≡-refl , ≋[ 𝒫 ]-sym p)

◇'-≋-trans : Transitive (_◇'-≋_ {𝒫} {w})
◇'-≋-trans {𝒫} (proof (≡-refl , ≡-refl , p)) (proof (≡-refl , ≡-refl , q)) = proof (≡-refl , ≡-refl , ≋[ 𝒫 ]-trans p q)

≡-to-◇'-≋ : {x y : ◇'-Fam 𝒫 w} → x ≡ y → x ◇'-≋ y
≡-to-◇'-≋ ≡-refl = ◇'-≋-refl

◇'-≋[]-syn : (𝒫 : Psh) → (x y : ◇'-Fam 𝒫 w) → Set
◇'-≋[]-syn {w = w} 𝒫 = _◇'-≋_ {𝒫} {w}

syntax ◇'-≋[]-syn 𝒫 x y = x ◇'-≋[ 𝒫 ] y

---------------------
-- ◇' 𝒫 is a presheaf
---------------------

◇'_ : (𝒫 : Psh) → Psh 
◇' 𝒫 = record
         { Fam           = ◇'-Fam 𝒫
         ; _≋_           = _◇'-≋_
         ; ≋-equiv       = λ _ → ◇'-≋-equiv
         ; wk            = wk-◇'
         ; wk-pres-≋     = wk-◇'-pres-≋
         ; wk-pres-refl  = wk-◇'-pres-refl
         ; wk-pres-trans = wk-◇'-pres-trans
         }
   where

   ◇'-≋-equiv : IsEquivalence (_◇'-≋_ {𝒫} {w})
   ◇'-≋-equiv = record
     { refl  = ◇'-≋-refl
     ; sym   = ◇'-≋-sym
     ; trans = ◇'-≋-trans
     }
  
   wk-◇' : w ⊆ w' → ◇'-Fam 𝒫 w → ◇'-Fam 𝒫 w'
   wk-◇' i (elem (v , r , p)) = elem (factorW i r , (factorR i r) , wk[ 𝒫 ] (factor⊆ i r) p) 

   abstract
     wk-◇'-pres-≋ : (i : w ⊆ w') {x y : ◇'-Fam 𝒫 w} → x ◇'-≋ y → wk-◇' i x ◇'-≋ wk-◇' i y
     wk-◇'-pres-≋ i {x = elem (v , r , p)} (proof (≡-refl , ≡-refl , p≋p')) = proof (≡-refl , ≡-refl , (wk[ 𝒫 ]-pres-≋ (factor⊆ i r) p≋p'))

     wk-◇'-pres-refl : (x : ◇'-Fam 𝒫 w) → wk-◇' ⊆-refl x ◇'-≋ x
     wk-◇'-pres-refl (elem (v , r , p)) rewrite factor-pres-⊆-refl r = proof (≡-refl , (≡-refl , (wk[ 𝒫 ]-pres-refl p)))

     wk-◇'-pres-trans : (i : w ⊆ w') (i' : w' ⊆ w'') (x : ◇'-Fam 𝒫 w)
       → wk-◇' (⊆-trans i i') x ◇'-≋ wk-◇' i' (wk-◇' i x)
     wk-◇'-pres-trans i i' (elem (v , r , p)) rewrite factor-pres-⊆-trans i i' r = proof (≡-refl , (≡-refl , wk[ 𝒫 ]-pres-trans (factor⊆ i r) (factor⊆ i' (factorR i r)) p))

---------------------------
-- ◇' is a presheaf functor
---------------------------

◇'-map-fun : (t : 𝒫 →̇ 𝒬) → ({w : C} → ◇'-Fam 𝒫 w → ◇'-Fam 𝒬 w)
◇'-map-fun t (elem (v , r , p)) = elem (v , r , t .apply p)

abstract
    ◇'-map-fun-pres-≋ : (t : 𝒫 →̇ 𝒬) → {p p' : ◇'-Fam 𝒫 w} → p ◇'-≋ p' → (◇'-map-fun t p) ◇'-≋ (◇'-map-fun t p')
    ◇'-map-fun-pres-≋ t (proof (≡-refl , ≡-refl , p≋p')) = proof (≡-refl , ≡-refl , t .apply-≋ p≋p')

    ◇'-map-fun-pres-≈̇ : {t t' : 𝒫 →̇ 𝒬} → t ≈̇ t' → (p : ◇'-Fam 𝒫 w) → ◇'-map-fun t p ◇'-≋ ◇'-map-fun t' p
    ◇'-map-fun-pres-≈̇ {𝒫} t≈̇t' (elem (v , r , p)) = proof (≡-refl , (≡-refl , apply-sq t≈̇t' ≋[ 𝒫 ]-refl))

    ◇'-map-natural : (t : 𝒫 →̇ 𝒬) (i : w ⊆ v) (p : (◇' 𝒫) ₀ w)
      → wk[ ◇' 𝒬 ] i (◇'-map-fun t p) ≋[ ◇' 𝒬 ] ◇'-map-fun t (wk[ ◇' 𝒫 ] i p)
    ◇'-map-natural t w (elem (v , r , p)) = proof (≡-refl , (≡-refl , t .natural (factor⊆ w r) p))

◇'-map_ : {𝒫 𝒬 : Psh} → (t : 𝒫 →̇ 𝒬) → (◇' 𝒫 →̇ ◇' 𝒬)
◇'-map_ {𝒫} {𝒬} t = record
  { fun     = ◇'-map-fun t
  ; pres-≋  = ◇'-map-fun-pres-≋ t
  ; natural = ◇'-map-natural t
  }

◇'-is-PshFunctor : EndoFunctor PshCat
◇'-is-PshFunctor = record
                    { ℱ'_            = ◇'_
                    ; ℱ'-map_        = ◇'-map_
                    ; ℱ'-map-pres-≈̇  = ◇'-map-pres-≈̇
                    ; ℱ'-map-pres-id = ◇'-map-pres-id
                    ; ℱ'-map-pres-∘  = ◇'-map-pres-∘
                    }
  where
  abstract
    ◇'-map-pres-≈̇ : {𝒫 𝒬 : Psh} {t t' : 𝒫 →̇ 𝒬} → t ≈̇ t' → ◇'-map t ≈̇ ◇'-map t'
    ◇'-map-pres-≈̇ t≈̇t' = record { proof = λ p → ◇'-map-fun-pres-≈̇ t≈̇t' p }

    ◇'-map-pres-id : {𝒫 : Psh} → ◇'-map id'[ 𝒫 ] ≈̇ id'
    ◇'-map-pres-id = record { proof = λ p → ◇'-≋-refl }

    ◇'-map-pres-∘ : {𝒫 𝒬 ℛ : Psh} (t' : 𝒬 →̇ ℛ) (t : 𝒫 →̇ 𝒬) → ◇'-map (t' ∘ t) ≈̇ ◇'-map t' ∘ ◇'-map t
    ◇'-map-pres-∘ _t' _t = record { proof = λ p → ◇'-≋-refl }




